/*
 Jenkins Declarative Pipeline
 ----------------------------
 Purpose:
 - Fetch source code from GitHub
 - Build and package a Maven application
 - Run static code analysis (Checkstyle & SonarQube)
 - Build and push Docker image to AWS ECR
 - Deploy updated image to AWS ECS

 Prerequisites:
 - Jenkins with Maven, JDK, Docker, AWS CLI configured
 - SonarQube server integrated with Jenkins
 - AWS credentials stored securely in Jenkins
 - ECR repository and ECS cluster/service pre-created
*/

/*Define slack notification color maps */
def COLOR_MAP = [
    'SUCCESS': 'good', 
    'FAILURE': 'danger',
    'UNSTABLE': 'warning',
    'ABORTED': 'danger'
]

pipeline {

    /* Run pipeline on any available Jenkins agent */
    agent any

    /* Define build tools configured in Jenkins global tools */
    tools {
        maven "MAVEN3.9"
        jdk "JDK17"
    }

    /* Global environment variables */
    environment {
        registryCredential = 'ecr:us-east-1:awscreds'
        appRegistry        = "061039793545.dkr.ecr.us-east-1.amazonaws.com/vprofile-app-img"
        vprofileRegistry   = "https://061039793545.dkr.ecr.us-east-1.amazonaws.com/"
        cluster            = "vprofile"
        service            = "vprofileapp-svc"
    }

    stages {

        /* Clone Java application source code from GitHub */
        stage('Fetch Code') {
            steps {
                git branch: 'docker',
                    url: 'https://github.com/hkhcoder/vprofile-project.git'
            }
        }

        /* Build and package the application using Maven */
        stage('Build Application') {
            steps {
                sh 'mvn install -DskipTests'
            }
            post {
                success {
                    echo 'Archiving generated WAR file'
                    archiveArtifacts artifacts: '**/target/*.war'
                }
            }
        }

        /* Perform static code analysis using Checkstyle */
        stage('Checkstyle Analysis') {
            steps {
                sh 'mvn checkstyle:checkstyle'
            }
        }

        /* Perform code quality analysis using SonarQube */
        stage('SonarQube Analysis') {
            environment {
                scannerHome = tool 'SONAR7.3'
            }
            steps {
                withSonarQubeEnv('SonarQube-Server') {
                    sh """
                    ${scannerHome}/bin/sonar-scanner \
                    -Dsonar.projectKey=vprofile \
                    -Dsonar.projectName=vprofile \
                    -Dsonar.projectVersion=1.0 \
                    -Dsonar.sources=src/ \
                    -Dsonar.java.binaries=target/test-classes/ \
                    -Dsonar.junit.reportsPath=target/surefire-reports/ \
                    -Dsonar.jacoco.reportsPath=target/jacoco.exec \
                    -Dsonar.java.checkstyle.reportPaths=target/checkstyle-result.xml
                    """
                }
            }
        }

        /* Build Docker image using a multi-stage Dockerfile */
        stage('Build Docker Image') {
            steps {
                script {
                    dockerImage = docker.build(
                        "${appRegistry}:${BUILD_NUMBER}",
                        "./Docker-files/app/multistage/"
                    )
                }
            }
        }

        /* Push Docker image to AWS ECR */
        stage('Push Image to ECR') {
            steps {
                script {
                    docker.withRegistry(vprofileRegistry, registryCredential) {
                        dockerImage.push("${BUILD_NUMBER}")
                        dockerImage.push("latest")
                    }
                }
            }
        }

        /* Cleanup unused Docker images to save up on disk space */
        stage('Cleanup Docker Images') {
            steps {
                sh 'docker rmi -f $(docker images -a -q)'
            }
        }

        /* Deploy latest image to AWS ECS service */
        stage('Deploy to ECS') {
            steps {
                withAWS(credentials: 'awscreds', region: 'us-east-1') {
                    sh '''
                    aws ecs update-service \
                      --cluster ${cluster} \
                      --service ${service} \
                      --force-new-deployment
                    '''
                }
            }
        }
    }
    post {
        always {
            echo 'Slack Notifications.'
            slackSend   channel: '#jenkins-cicd',
                        color: COLOR_MAP[currentBuild.currentResult],
                        message: "*${currentBuild.currentResult}:* Job ${env.JOB_NAME} build ${env.BUILD_NUMBER} \n More info at: ${env.BUILD_URL}"
        }
    }
}
